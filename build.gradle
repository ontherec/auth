plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.epages.restdocs-api-spec' version '0.19.4'
}

group = 'kr.ontherec'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	implementation {
		exclude group: 'ch.qos.logback'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// spring boot
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	// db
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'com.mysql:mysql-connector-j'

	// dev
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// mapper
	implementation 'org.mapstruct:mapstruct:1.6.2'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testCompileOnly 'org.assertj:assertj-core'
	testImplementation 'io.rest-assured:rest-assured'
	testImplementation 'org.testcontainers:testcontainers:1.20.4'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
	testImplementation 'org.testcontainers:mysql'
	testImplementation 'com.tngtech.archunit:archunit-junit5-api:1.3.0'
	testRuntimeOnly 'com.tngtech.archunit:archunit-junit5-engine:1.3.0'

	// docs
	testImplementation 'org.springframework.restdocs:spring-restdocs-restassured:3.0.3'
	testImplementation 'com.epages:restdocs-api-spec-restassured:0.19.4'

	// logging
	implementation 'org.tinylog:tinylog-api:2.7.0'
	implementation 'org.tinylog:tinylog-impl:2.7.0'
	implementation 'org.tinylog:slf4j-tinylog:2.7.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

openapi3 {
	server = 'https://accounts.ontherec.live'
	title = 'On The Record Authorization Server'
	description = '개발 서버에서 제공되는 On The Record 인증 서버 API 문서 입니다.'
	version = '0.3.0'
	format = 'json'
}

tasks.register('docs', Copy) {
	dependsOn 'openapi3'
	delete 'src/main/resources/static/openapi3.json'
	from file("build/api-spec")
	into file("src/main/resources/static")
}